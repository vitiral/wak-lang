WHITESPACE = _{ " " | "\t" | "\n" | "\r\n" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }

file = _{
  SOI
  ~ (declare ~ ";"?)*
  ~ EOI
}

expr = { 
  expr_item ~ (
    operation
    | expand1
    | expand2
  )*
}

expr_item = { value | closed | declare | iden }

////////////////
// Operation

operation = { operator ~ expr }

ACCESS      = { "." }
CALL        = { "$" }

operator = _{ ACCESS | CALL }

////////////////
// Macro Expansion
expand1 = { EXPAND1 ~ expand_item }
expand2 = { EXPAND2 ~ expr_item ~ expand_item }

EXPAND1     = _{ "!" }
EXPAND2     = _{ "!!" }

expand_item = _{ expr_item | arbitrary }

////////////////
// Keywords
LET = { "let" }
VISIBILITY = { "pub" }
OWNERSHIP = { "const" | "own" | "&" | "excl" }
END = { ";" }

///////////////////
// Identifiers
iden = @{ (!(ASCII_DIGIT) ~ iden_char) ~ iden_char* }
iden_char = @{ "_" | ASCII_ALPHANUMERIC }

////////////////
// Value Literals
value = { bool | float | integer | char | string | raw_string }

bool = { "true" | "false" }

float = @{
  ("+"|"-")?
  ~ ASCII_DIGIT ~ DIGIT*
  ~ "." ~  DIGIT*
  ~ (^"e" ~ ("-"|"+")? ~ DIGIT+)?
}

integer = @{
  ("+" | "-")?
  ~ (
    "0x" ~ DIGIT_HEX+
    | ASCII_DIGIT ~ DIGIT*
  )
}

char = {
  "'" ~ string_char ~ "'"
}

raw_string = ${
  "''" ~ PUSH("'"*)  // push the number of `'` onto the stack
  ~ raw_string_inner
  ~ "''" ~ POP
}

raw_string_inner = @{
  (
    // Consume any character unless the next
    // characters are the correct number of single-quotes
    !("''" ~ PEEK)
    ~ ANY
  )
}

string = ${
  "\"" ~ string_inner ~ "\""
}

string_inner = @{ string_char* }
string_char = @{
  !("\"" | "\\") ~ ANY
  | "\\" ~ ("\\" | "\"" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

DIGIT = @{ASCII_DIGIT | "_"}
DIGIT_HEX = @{ASCII_HEX_DIGIT | "_"}

////////////////
// Declaration

declare = { declare_fn /*| declare_struct | declare_interface | declare_impl*/ | declare_var }
declare_fn = { "fn" ~ VISIBILITY? ~ iden ~ data ~ ("->" ~ data)? ~ block }
declare_var = { 
  LET ~ VISIBILITY? ~ iden ~ type_? ~ ("=" ~ expr)?
  | iden ~ type_? ~ "=" ~ expr
}

////////////////
// Closed Expressions

closed = { block | data | type_ }

block = { 
   "(" ~ (expr ~ END)* ~ (expr ~ END?)? ~ ")"
}

data = { "{" ~ (declare_var  ~ ";")* ~ (declare_var ~ ";"?)? ~ "}" }

type_ = { "[" ~ integer? ~ OWNERSHIP* ~ type_inner ~ "]" }
type_inner = { (expr ~ type_? ~ ";")* ~ expr ~ type_? ~ ";"?  }

arbitrary = { "(" ~ arbitrary_inner* ~ ")" }
arbitrary_inner = _{ value | arbitrary | arbitrary_token }
arbitrary_token = @{ ANY+ }

