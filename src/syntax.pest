WHITESPACE = _{ " " | "\t" | "\n" | "\r\n" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }

file = _{
  SOI
  ~ (declare ~ ";"?)*
  ~ EOI
}

expr = {
  value
  | closed
  | call_fn
  | declare
  | fullname
}

////////////////
// Keywords
LET = { "let" }
VISIBILITY = { "pub" }
OWNERSHIP = { "own" | "mut" | "sync" | "const" }
END = { ";" }

///////////////////
// Identifiers
identifier = _{  /* call_macro1 |*/ fullname }
fullname = @{ (!(ASCII_DIGIT | ".") ~ fullname_char) ~ fullname_char* }
var = @{ (!ASCII_DIGIT ~ var_char) ~ var_char* }

var_char = @{ "_" | ASCII_ALPHANUMERIC }
fullname_char = @{"." | var_char }

////////////////
// Value Literals
value = { bool | float | integer | char | string | raw_string }

bool = { "true" | "false" }

float = @{
  ("+"|"-")?
  ~ ASCII_DIGIT ~ DIGIT*
  ~ "." ~  DIGIT*
  ~ (^"e" ~ ("-"|"+")? ~ DIGIT+)?
}

integer = @{
  ("+" | "-")?
  ~ (
    "0x" ~ DIGIT_HEX+
    | ASCII_DIGIT ~ DIGIT*
  )
}

char = {
  "'" ~ string_char ~ "'"
}

raw_string = ${
  "''" ~ PUSH("'"*)  // push the number of `'` onto the stack
  ~ raw_string_inner
  ~ "''" ~ POP
}

raw_string_inner = @{
  (
    // Consume any character unless the next
    // characters are the correct number of single-quotes
    !("''" ~ PEEK)
    ~ ANY
  )
}

string = ${
  "\"" ~ string_inner ~ "\""
}

string_inner = @{ string_char* }
string_char = @{
  !("\"" | "\\") ~ ANY
  | "\\" ~ ("\\" | "\"" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

DIGIT = @{ASCII_DIGIT | "_"}
DIGIT_HEX = @{ASCII_HEX_DIGIT | "_"}

////////////////
// Declaration

declare = { declare_fn /*| declare_struct | declare_interface | declare_impl*/ | declare_var }
declare_fn = { "fn" ~ VISIBILITY? ~ fullname ~ data ~ ("->" ~ data)? ~ block }
declare_var = { 
  LET? ~ VISIBILITY? ~ OWNERSHIP* ~ var ~ type_? ~ ("=" ~ OWNERSHIP* ~ expr)?
}


////////////////
// Closed Expressions

closed = _{ block | data | type_ }

block = { 
   "(" ~ (expr ~ END)* ~ (expr ~ END?)? ~ ")"
}

data = { "{" ~ (declare_var  ~ ";")* ~ (declare_var ~ ";"?)? ~ "}" }

type_ = { "[" ~ type_inner ~ "]" }
type_inner = { (identifier ~ type_? ~ ";")* ~ identifier ~ type_? ~ ";"?  }

////////////////
// Special Expressions
call_fn = { fullname ~ data }
